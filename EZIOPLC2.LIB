/***

PLC-bus specific stuff for Ezio. (16-bit address for PLC-bus ports)

***/

/*** BeginHeader */

#ifndef _EZIOPLC2_LIB
#define _EZIOPLC2_LIB

#use eziopbdv.lib

/*** EndHeader */

/*** BeginHeader _eioReadD0 */

char _eioReadD0();

/*** EndHeader */

/* START FUNCTION DESCRIPTION ********************************************
_eioReadD0

SYNTAX: 	char _eioReadD0();

DESCRIPTION:	this function reads the data on the PLC-bus in the BUSRD0
cycle.

RETURN VALUE:	the byte read on the PLC-bus in the BUSRD0 cycle

END DESCRIPTION **********************************************************/

#asm
_eioReadD0::			;	16
	push	bc
	ld		bc,BUSRD0
	in		a,(c)
;	in0	a,(BUSRD0)	;	12
	ld		l,a			;	4
	ld		h,0			;	6
	pop	bc
	ret					;	9
	;	47 states
#endasm

/*** BeginHeader _eioReadD1 */

char _eioReadD1();

/*** EndHeader */

/* START FUNCTION DESCRIPTION ********************************************
_eioReadD1

SYNTAX: 	char _eioReadD1();

DESCRIPTION:	this function reads the data on the PLC-bus in the BUSRD1
cycle.

RETURN VALUE:	the byte read on the PLC-bus in the BUSRD1 cycle

END DESCRIPTION **********************************************************/

#asm
_eioReadD1::
	push	bc
	ld		bc,BUSRD1
	in		a,(c)
;	in0	a,(BUSRD1)
	ld		l,a
	ld		h,0
	pop	bc
	ret
	;	47 states
#endasm

/*** BeginHeader _eioReadD2 */

char _eioReadD2();

/*** EndHeader */

/* START FUNCTION DESCRIPTION ********************************************
_eioReadD2

SYNTAX: 	char _eioReadD2();

DESCRIPTION:	this function reads the data on the PLC-bus in the BUSRD2
cycle.

RETURN VALUE:	the byte read on the PLC-bus in the BUSRD2 cycle

END DESCRIPTION **********************************************************/

#asm
_eioReadD2::
	push	bc
	ld		bc,BUSRD2
	in		a,(c)
;	in0	a,(BUSRD2)
	ld		l,a
	ld		h,0
	pop	bc
	ret
	;	47
#endasm

/*** BeginHeader _eioWriteWR */

void _eioWriteWR(char ch);

/*** EndHeader */

/* START FUNCTION DESCRIPTION ********************************************
_eioWriteWR

SYNTAX: 	void _eioWriteWR(char ch);

DESCRIPTION:	this function writes information to the PLC-bus during the
BUSWR cycle.

PARAMETER1:	this is the byte to be written to the PLC-bus

RETURN VALUE:	N/A

END DESCRIPTION **********************************************************/

#asm
_eioWriteWR::			;	16
	push	bc
	ld		a,l			;	4
	ld		bc,BUSWR
	out	(c),a
;	out0	(BUSWR),a	;	13
	pop	bc
	ret					;	9
	;	42
#endasm

/*** BeginHeader eioResetPlcBus */

void eioResetPlcBus();

/*** EndHeader */

/* START FUNCTION DESCRIPTION ********************************************
eioResetPlcBus

SYNTAX: 	void eioResetPlcBus();

DESCRIPTION:	this function resets all boards on the PLC-bus. The
programmer must make sure there is enough delay between this call and
the first access to any PLC-bus devices for the reset to take effect.

RETURN VALUE:	N/A

END DESCRIPTION **********************************************************/

#ifndef BUSRST
#define BUSRST RST8
#endif

#asm
eioResetPlcBus::
	push	bc
	ld		bc,BUSRST
	in		a,(c)
	pop	bc
	ret
#endasm

/*** BeginHeader eioPlcAdr12 */

void eioPlcAdr12(unsigned addr);

/*** EndHeader */

/* START FUNCTION DESCRIPTION ********************************************
eioPlcAdr12

SYNTAX: 	void eioPlcAdr12(unsigned addr);

DESCRIPTION:	this function specifies an address on the PLC-bus using
cycles BUSADR0, BUSADR1 and BUSADR2. "addr" is broken into three nibbles,
and one nibble is written in each BUSADRx cycle.

PARAMETER1:	this is the address to be written to the PLC-bus.

RETURN VALUE:	N/A

END DESCRIPTION **********************************************************/

#asm
eioPlcAdr12::
	;	addr in HL
	push	bc
	push	de
	ex		de,hl
	ld		bc,BUSADR0
	ld		hl,SHBUS0+1
	ld		(hl),e
	out	(c),e
	srl	e
	srl	e
	srl	e
	srl	e
	ld		bc,BUSADR1
	inc	hl
	ld		(hl),e
	out	(c),e
	ld		bc,BUSADR2
	inc	hl
	ld		(hl),d
	out	(c),d
	pop	de
	pop	bc
	ret
#endasm

/*** BeginHeader eioPlcAdr4 */

void eioPlcAdr4(unsigned addr);

/*** Endheader */

/* START FUNCTION DESCRIPTION ********************************************
eioPlcAdr4

SYNTAX: 	void eioPlcAdr4(unsigned addr);

DESCRIPTION:	this function specifies an address on the PLC-bus using
cycle BUSADR2 only. .

PARAMETER1:	this is the nibble corresponding to ADR2.

RETURN VALUE:	N/A

END DESCRIPTION **********************************************************/

#asm
eioPlcAdr4::
	push	bc
	ld		bc,BUSADR2
	ld		a,l
	ld		(SHBUS0+3),a
	out	(c),l
	pop	bc
	ret
#endasm

/*** BeginHeader eioPlcAdr24, eioPlcAdr8 */

void eioPlcAdr24(unsigned long ul);

void eioPlcAdr8(unsigned u);

/*** EndHeader */

#asm
eioPlcAdr24::                     ; set 24-bit addr in 3-bytes addr in BCDE
	ld hl,SHBUS0+1
	ld (hl),c                   ; shadow BUSADR0
	ld		a,c
	ld		bc,BUSADR0
	out	(c),a

	inc   hl
	ld (hl),d                   ; shadow BUSADR1
	ld		bc,BUSADR1
	out	(c),d
	;

eioPlcAdr8::
	ld hl,SHBUS0+3
	ld (hl),e                   ;3rd byte
	ld		bc,BUSADR2
	out	(c),e
	ret
#endasm

/*** BeginHeader _eioClockOut */

/*
dataword		hl
bit count	a
data0			d
data1			e
clk0			d'
clk1			e'
*/

/*** EndHeader */

#asm
_eioClockOut::
	ld		bc,BUSWR
	push	bc
	exx
	pop	bc
	exx
outAgain:
	bit	7,h
	jr		z,data0
;	data1
	out	(c),e
	jr		clock10
data0:
	out	(c),d
clock10:
	exx
	out	(c),e
	out	(c),d
	exx
	add	hl,hl
	dec	a
	jr		nz,outAgain
	ret	
#endasm

/*** BeginHeader _eioClockInD0 */

/*
dataword		hl
bit count	a
data1			d
clk0			d'
clk1			e'
*/

/*** EndHeader */

#asm
_eioClockInD0::
	ld		bc,BUSRD0
	exx
	ld		bc,BUSWR
inAgain:
	out	(c),e
	exx
	add	hl,hl
	ex		af,af'
	in		a,(c)
	and	d
	jr		z,data0
	inc	hl
data0:
	ex		af,af'
	exx
	out	(c),d
	dec	a
	jr		nz,inAgain
	exx
	ret
#endasm

/*** BeginHeader _eioClockOutM */

/*
dataword		hl
bit count	a
data0			d
data1			e
clk			d'		bits to toggle for clock
*/

/*** EndHeader */

#asm
_eioClockOutM::
	ld		bc,BUSWR
	push	bc
	exx
	pop	bc
	exx
outAgain:
	bit	7,h
	jr		z,data0
;	data1
	out	(c),e
	push	af
	ld		a,e
	jr		clock10
data0:
	out	(c),d
	push	af
	ld		a,d
clock10:
	exx
	xor	d
	out	(c),a
	xor	d
	out	(c),a
	pop	af
	exx
	add	hl,hl
	dec	a
	jr		nz,outAgain
	ret	
#endasm

/*** beginHeader */

#endif

/*** EndHeader */